/* The Computer Language Shootout  
   http://shootout.alioth.debian.org/
   contributed by John van Groningen (Clean Guru!)
*/


module wc
import StdEnv

Start world
	# (console, world) = stdio world
	# (nl, nw, nc) = count_file console 0 0 0 False
	= toString nl +++ " " +++ toString nw +++ " " +++ toString nc +++ "\n"

count_file :: !*File !Int !Int !Int !Bool -> .(Int,Int,Int)
count_file f nc nw nl inside
	# (s,f) = freads f 4096
	# n=size s
	| n==0
		= (nl, nw, nc)
		# (nw,nl,inside) = if inside
							(count_sw 0 n nw nl s)
							(count_s 0 n nw nl s)
		= count_file f (nc+n) nw nl inside

count_s :: !Int !Int !Int !Int !{#Char} -> (!Int,!Int,!Bool)
count_s i n nw nl s
	| i<n
	    # c=s.[i]
	    | c==' ' || c=='\t'
          	= count_s (i+1) n nw nl s
	    | c=='\n'
        	= count_s (i+1) n nw (nl+1) s
          	= count_sw (i+1) n (nw+1) nl s
		= (nw,nl,False)

count_sw :: !Int !Int !Int !Int !{#Char} -> (!Int,!Int,!Bool)
count_sw i n nw nl s
	| i<n
	    | s.[i]>' '
          	= count_sw (i+1) n nw nl s
	    # c=s.[i]
	    | c==' ' || c=='\t'
          	= count_s (i+1) n nw nl s
	    | s.[i]=='\n'
          	= count_s (i+1) n nw (nl+1) s
          	= count_sw (i+1) n nw nl s
		= (nw,nl,True)
