-- The Great Computer Language Shootout
-- http://shootout.alioth.debian.org/
-- for se from jose

--SPLITFILE=dispatch.e

class DISPATCH insert ARGUMENTS creation make feature
	make is local i,n,ctrl:INTEGER do
		if argument_count>=1 and then argument(1).is_integer then
			n:=argument(1).to_integer end
		from until i>=n loop ctrl:=ctrl+test(b1,b2,b3,b4,b5,i) +
				test(b6,b7,b8,b9,b0,i)+test(p1,p2,p3,p4,p5,i)-
				test(p6,p7,p8,p9,p0,i) 
			i:=i+1 end
		print(ctrl.out+"%N") end
	test(a1,a2,a3,a4,a5:BOTTLE;i:INTEGER):INTEGER is local c:INTEGER do
		a1.cycle a2.cycle a3.cycle a4.cycle a5.cycle c:=i\\2
		Result:=a1.test(c)+a2.test(c)+a3.test(c)+a4.test(c)+
			a5.test(c) end
	b1:BOTTLE is once create Result.make(1) end
	b2:BOTTLE is once create Result.make(2) end
	b3:BOTTLE is once create Result.make(3) end
	b4:BOTTLE is once create Result.make(4) end
	b5:BOTTLE is once create Result.make(5) end
	b6:BOTTLE is once create Result.make(6) end
	b7:BOTTLE is once create Result.make(7) end
	b8:BOTTLE is once create Result.make(8) end
	b9:BOTTLE is once create Result.make(9) end
	b0:BOTTLE is once create Result.make(0) end
	p1:PRESSURIZED_BOTTLE is once create Result.make(1) end
	p2:PRESSURIZED_BOTTLE is once create Result.make(2) end
	p3:PRESSURIZED_BOTTLE is once create Result.make(3) end
	p4:PRESSURIZED_BOTTLE is once create Result.make(4) end
	p5:PRESSURIZED_BOTTLE is once create Result.make(5) end
	p6:PRESSURIZED_BOTTLE is once create Result.make(6) end
	p7:PRESSURIZED_BOTTLE is once create Result.make(7) end
	p8:PRESSURIZED_BOTTLE is once create Result.make(8) end
	p9:PRESSURIZED_BOTTLE is once create Result.make(9) end
	p0:PRESSURIZED_BOTTLE is once create Result.make(0) end
end

--SPLITFILE=bottle.e

class BOTTLE insert BOTTLE_STATE_CONSTANTS creation make feature
	id:INTEGER
	state:BOTTLE_STATE
	make(i:INTEGER) is do id:=i state:=initial_state end
	cycle is do fill seal empty end
	fill, seal, empty is do state:=state.next end
	test(c: INTEGER): INTEGER is do Result:=state.tag+id+c end
end

--SPLITFILE=bottle_state_constants.e

class BOTTLE_STATE_CONSTANTS feature
	initial_state: BOTTLE_STATE is do Result:=empty_state end
	empty_state: EMPTY_BOTTLE_STATE is once create Result end
	full_state: FULL_BOTTLE_STATE is once create Result end
	sealed_state: SEALED_BOTTLE_STATE is once create Result end
end

--SPLITFILE=bottle_state.e

deferred class BOTTLE_STATE insert BOTTLE_STATE_CONSTANTS feature
	tag: INTEGER is deferred end
	next: BOTTLE_STATE is deferred end
end

--SPLITFILE=empty_bottle_state.e

class EMPTY_BOTTLE_STATE inherit BOTTLE_STATE feature
	tag: INTEGER is 1
	next: BOTTLE_STATE is do Result:=full_state end
end

--SPLITFILE=full_bottle_state.e

class FULL_BOTTLE_STATE inherit BOTTLE_STATE feature
	tag: INTEGER is 2
	next: BOTTLE_STATE is do Result:=sealed_state end
end

--SPLITFILE=sealed_bottle_state.e

class SEALED_BOTTLE_STATE inherit BOTTLE_STATE feature
	tag: INTEGER is 3
	next: BOTTLE_STATE is do Result:=empty_state end
end

--SPLITFILE=pressurized_bottle_state_constants.e

class PRESSURIZED_BOTTLE_STATE_CONSTANTS insert
	BOTTLE_STATE_CONSTANTS redefine initial_state end
feature
	initial_state: PRESSURIZED_BOTTLE_STATE is do
		Result:=unpressurized_empty_state end
	unpressurized_empty_state: UNPRESSURIZED_EMPTY_BOTTLE_STATE is once
		create Result end
	unpressurized_full_state: UNPRESSURIZED_FULL_BOTTLE_STATE is once
		create Result end
	pressurized_unsealed_state: PRESSURIZED_UNSEALED_BOTTLE_STATE is once
		create Result end
	pressurized_sealed_state: PRESSURIZED_SEALED_BOTTLE_STATE is once
		create Result end
end

--SPLITFILE=pressurized_bottle_state.e

deferred class PRESSURIZED_BOTTLE_STATE inherit
	BOTTLE_STATE undefine initial_state end
insert
	PRESSURIZED_BOTTLE_STATE_CONSTANTS
feature
	next: PRESSURIZED_BOTTLE_STATE is deferred end
end

--SPLITFILE=unpressurized_empty_bottle_state.e

class UNPRESSURIZED_EMPTY_BOTTLE_STATE inherit PRESSURIZED_BOTTLE_STATE feature
	tag: INTEGER is 4
	next: PRESSURIZED_BOTTLE_STATE is do
		Result:=unpressurized_full_state end
end

--SPLITFILE=unpressurized_full_bottle_state.e

class UNPRESSURIZED_FULL_BOTTLE_STATE inherit PRESSURIZED_BOTTLE_STATE feature
	tag: INTEGER is 5
	next: PRESSURIZED_BOTTLE_STATE is do
		Result:=pressurized_unsealed_state end
end

--SPLITFILE=pressurized_unsealed_bottle_state.e

class PRESSURIZED_UNSEALED_BOTTLE_STATE inherit PRESSURIZED_BOTTLE_STATE feature
	tag: INTEGER is 6
	next: PRESSURIZED_BOTTLE_STATE is do
		Result:=pressurized_sealed_state end
end

--SPLITFILE=pressurized_sealed_bottle_state.e

class PRESSURIZED_SEALED_BOTTLE_STATE inherit PRESSURIZED_BOTTLE_STATE feature
	tag: INTEGER is 7
	next: PRESSURIZED_BOTTLE_STATE is do
		Result:=unpressurized_empty_state end
end

--SPLITFILE=pressurized_bottle.e

class PRESSURIZED_BOTTLE inherit
	BOTTLE undefine initial_state redefine cycle end
insert
	PRESSURIZED_BOTTLE_STATE_CONSTANTS
creation make feature
	cycle is do fill pressurize seal empty end
	pressurize is do state:=state.next end
end

