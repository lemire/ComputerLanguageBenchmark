' The Computer Language Shootout
' http://shootout.alioth.debian.org/
' contributed by Josh Goldfoot
' small update to ByVal, and printf by yetifoot

#include "crt.bi"

Option Escape

Function Ack(ByVal x As Integer, ByVal y As Integer) As Integer
  If x = 0 Then Return (y + 1)
  If y = 0 Then Return (Ack(x-1, 1))
  Return (Ack(x-1, Ack(x, y-1)))
End Function

Function Fib(ByVal n As Integer) As Integer
  If n < 2 Then Return 1
  Return Fib(n-2) + Fib(n-1)
End Function

Function FibFP(ByVal n As Double) As Double
  If n < 2.0 Then Return 1.0
  Return FibFP(n-2.0) + FibFP(n-1.0)
End Function

Function Tak(ByVal x As Integer, ByVal y As Integer, ByVal z As Integer) As Integer
  If y < x Then Return Tak( Tak(x-1, y, z), Tak(y-1, z, x), Tak(z-1, x, y) )
  Return z
End Function

Function TakFP(ByVal x As Double, ByVal y As Double, ByVal z As Double) As Double
  If y < x Then Return TakFP( TakFP(x-1.0, y, z), TakFP(y-1.0, z, x), TakFP(z-1.0, x, y) )
  Return z
End Function

Dim n As Integer

  n = Valint(Command$(1)) - 1
  If n < 1 Then n = 3
  printf("Ack(3,%d): %d\n", n+1, Ack(3, n+1))
  printf("Fib(%.1f): %.1f\n", 28.0+n, FibFP(28.0+n))
  printf("Tak(%d,%d,%d): %d\n", 3*n, 2*n, n, Tak(3*n, 2*n, n))
  printf("Fib(3): %d\n", Fib(3))
  printf("Tak(3.0,2.0,1.0): %.1f\n", TakFP(3.0, 2.0, 1.0))
