/* ------------------------------------------------------------------ */
/* The Great Computer Language Shootout                               */
/* http://shootout.alioth.debian.org/                                 */
/*                                                                    */
/* Code based on / inspired by existing, relevant Shootout submissions*/
/*                                                                    */
/* Contributed by Anthony Borla                                       */
/* ------------------------------------------------------------------ */

N = ARG(1) ; if DATATYPE(N) \= 'NUM' | N < 1 then ; N = 1

say "Ack(3," || N || "):" FORMAT(ack(3, N),, 0)
say "Fib(" || 27.0 + N || "):" FORMAT(fib(27.0 + N),, 1)

N = N - 1
say "Tak(" || N * 3 || "," || N * 2 || "," || N || "):" FORMAT(tak(N * 3, N * 2, N),, 0)

say "Fib(3):" FORMAT(fib(3),, 0)
say "Tak(3.0,2.0,1.0):" FORMAT(tak(3.0, 2.0, 1.0),, 1)

exit 0

/* ----------------------------- */

ack :
  if ARG(1) == 0 then ; return ARG(2) + 1
  if ARG(2) == 0 then ; return ack(ARG(1) - 1, 1)
  return ack(ARG(1) - 1, ack(ARG(1), ARG(2) - 1))

/* ----------- */

fib :
  if ARG(1) < 2 then ; return 1
  return fib(ARG(1) - 2) + fib(ARG(1) - 1)

/* ----------- */

tak :
  if ARG(2) >= ARG(1) then ; return ARG(3)
  return tak(tak(ARG(1) - 1.0, ARG(2), ARG(3)), tak(ARG(2) - 1.0, ARG(3), ARG(1)), tak(ARG(3) - 1.0, ARG(1), ARG(2)))

