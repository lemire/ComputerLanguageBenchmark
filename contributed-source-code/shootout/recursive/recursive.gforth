\ Computer Language Shootout
\ http://shootout.alioth.debian.org/
\ must use flags "-d 8M -r 8M" when running gforth for NUM=10

\ read NUM from last command line argument
0. argc @ 1- arg >number 2drop drop 1- constant NUM

: ack ( y x -- n )
  ?dup if swap ?dup if 1- over recurse swap 1- recurse
                  else               1 swap 1- recurse then
     else 1+ then ;

: ffib ( f -- ffib )
  fdup 2e f< if fdrop 1e
           else 1e f- fdup recurse fswap 1e f- recurse f+ then ;

: fib ( n -- fib )
  dup 2 < if drop 1
        else 1- dup recurse swap 1- recurse + then ;

: tak { z y x -- n }
  y x < if
    y x z 1- recurse
    x z y 1- recurse
    z y x 1- recurse
    recurse
  else z then ;

: ftak { F: z F: y F: x -- n }
  y x f< if
    y x z 1e f- recurse
    x z y 1e f- recurse
    z y x 1e f- recurse
    recurse
  else z then ;

: ff.   f>d d>s 1 u.r ." .0" ;

NUM 1+   ." Ack(3," dup 1 u.r ." ): " 3 ack 1 u.r cr
NUM 28 + ." Fib(" dup 1 u.r ." .0): " 0 d>f ffib ff. cr
." Tak(" NUM 3 * 1 u.r ." ," NUM 2* 1 u.r ." ," NUM 1 u.r ." ): "
                     NUM dup 2* 2dup + tak 1 u.r cr
." Fib(3): " 3 fib 1 u.r cr
." Tak(3.0,2.0,1.0): " 1e 2e 3e ftak ff. cr

bye
