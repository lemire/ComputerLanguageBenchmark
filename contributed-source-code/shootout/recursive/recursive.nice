/* The Computer Language Shootout 
   http://shootout.alioth.debian.org/
   contributed by Isaac Gouy 
*/

import Include;

void main(String[] args){
   var n = Integer.parseInt(args[0]); 
   println("Ack(3," + n + "): " + ack(3,n)); 
   println("Fib(" + (27.0+n) + "): " + fib(27.0+n).toStringWith(1));
   n--; println("Tak("+3*n+","+2*n+","+n+"): "+ tak(3*n,2*n,n));

   println("Fib(3): " + fib(3));
   println("Tak(3.0,2.0,1.0): "+ tak(3.0,2.0,1.0));
}

int ack(int m,int n) =
   m == 0 ? n + 1 :
      n == 0 ? ack(m-1, 1) : ack(m-1, ack(m, n-1));

int fib(int n) = n < 2 ? 1 : fib(n-2) + fib(n-1);

int tak (int x, int y, int z) =
   y >= x ? z : tak( tak(x-1,y,z), tak(y-1,z,x), tak(z-1,x,y) ); 

double fib(double n) = n < 2.0 ? 1.0 : fib(n-2.0) + fib(n-1.0);

double tak (double x, double y, double z) =
   y >= x ? z : tak( tak(x-1.0,y,z), tak(y-1.0,z,x), tak(z-1.0,x,y) ); 
