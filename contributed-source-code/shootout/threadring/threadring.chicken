;; The Computer Language Benchmarks Game
;;  http://shootout.alioth.debian.org/
;; contributed by Graham Fawcett


(declare
 (uses srfi-1 srfi-18)
 (no-procedure-checks)
 (fixnum-arithmetic))

(define N (string->number (car (command-line-arguments))))

(define thread-act
  (lambda ()
    (let loop ((mbs (thread-specific (current-thread))))
      (let ((v (mailbox-receive! (cdr mbs))))
        (cond ((< v N)
               (mailbox-send! (car mbs) (add1 v))
               (loop mbs))
              (#t (print (thread-name (current-thread)))
                  (exit 0)))))))

(let* ((first-mb (make-mailbox "one"))
       (threads
        (let loop ((n 503) (acc '()) (prev-mb first-mb))
          (if (zero? n)
              (begin (set-cdr! (thread-specific (car acc)) first-mb)
                     acc)
              (let ((t (make-thread thread-act (->string n)))
                    (new-mb (make-mailbox (->string n))))
                (thread-specific-set! t (cons prev-mb new-mb))
                (loop (sub1 n) (cons t acc) new-mb))))))
  (for-each thread-start! threads)
  (mailbox-send! first-mb 0)
  (thread-join! (car threads)))


