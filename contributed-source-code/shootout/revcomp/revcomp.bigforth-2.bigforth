\ The Great Computer Language Shootout
\ http://shootout.alioth.debian.org/
\ contributed by Ian Osgood 
\ modified by Bernd Paysan
\ use option "-d 32M" to have enough memory to run this program

: mem,
  here over allot swap move ;

\ complement mappings
create map s" TVGHefCDijMlKNopqYSAABWxRz" mem,

: map-c ( c -- comp ) 32 or 'a - map + c@ ;

60 constant line-len
128 constant lines
line-len 1+ lines * Constant buf-size

\ data arrays
Variable linebuf buf-size allot

: flush, linebuf @+ swap type linebuf off ;
: emit, ( char -- )  linebuf @+ + c!  1 linebuf +! ;
: cr,  #lf emit, linebuf @ buf-size = IF
	flush,
    THEN ;

: rev-comp ( end -- start ) line-len
  begin  over pad u>
  while  swap 1- dup c@ map-c emit,
         swap 1- dup 0= if cr, line-len + then
  repeat line-len < if cr, then flush, ;

: main   pad ( end )
  begin  dup 80 stdin read-line throw 
  while  over c@ '> =
         if   over  rev-comp  -rot type cr
         else + then
  repeat drop rev-comp drop ;

main bye  \ done!

