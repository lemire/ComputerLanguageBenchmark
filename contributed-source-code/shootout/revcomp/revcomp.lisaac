/* The Computer Language Benchmarks Game
   http://shootout.alioth.debian.org/
   contributed by Xavier Oswald
*/

Section Header
  
  + name := REVCOMP;
  
Section Inherit
  
  - parent_any:OBJECT := OBJECT;

Section Public
      
  - iub_pairs:STRING_CONSTANT := "\0TVGH\0\00h\CD\0\0M\0KN\0\0\0YSAABW\0R";
    
  - main <-
  ( + buf:STRING;
    + size:INTEGER;
    + i,j,end:INTEGER;
    + tmp:CHARACTER;
   
    buf := STRING.create 25416746;
        
    size := FS_MIN.read `stdin`:POINTER in buf size 25416745;
    i := 1;
    {i <= size}.while_do {
      {buf.item i != '\n'}.while_do { i := i + 1; };
      i := i + 1;
      j := i;
      {(buf.item j != '>') && {buf.item j != 0}}.while_do { j := j + 1; };
      end := j;
      j := j - 1;
      {i < j}.while_do {
        (buf.item i = '\n').if { i := i + 1; };
        (buf.item j = '\n').if { j := j - 1; };
        tmp := iub_pairs.item ((buf.item i.to_integer & 1Fh)+1);
        buf.put (iub_pairs.item ((buf.item j.to_integer & 1Fh)+1)) to i;
        buf.put tmp to j;
        i := i + 1;
        j := j - 1;
      };
      i := end;
    };
    buf.remove_last 1;
    buf.println;        
  );
