(* The Computer Language Benchmarks Game
   http://shootout.alioth.debian.org/
   Contributed by David Teller
*)

let adder_thread (incoming, outgoing) =
  while true do
    Event.sync(Event.send outgoing (Event.sync(Event.receive incoming) + 1))
  done

let rec make_chain n cur =
  if n = 0 then cur else
    let next = Event.new_channel() in
    let _ = Thread.create adder_thread (cur, next) in
    make_chain (n-1) next

let rec sum chan0 chan1 total n =
  if n = 0 then total else (
    Event.sync(Event.send chan0 0);
    sum chan0 chan1 (total + Event.sync(Event.receive chan1)) (n-1)
  )

let () =
  let n = try int_of_string(Array.get Sys.argv 1) with _ -> 10
  and first_channel = Event.new_channel() in
  let last_channel  = make_chain 500 first_channel in
  print_int(sum first_channel last_channel 0 n); print_newline()
