(* methcall.sml
 *
 *   Simple "method call" benchmark.
 *
 * Copyright (c) 2004 by The Fellowship of SML/NJ
 *
 * Author: Matthias Blume (blume@tti-c.org)
 *)
structure Test:sig val main:string*string list->OS.Process.status end = struct

    (* the "methods" (work for both Toggle and NthToggle "objects") *)
    fun Activate (t as { a, r }) = (a (); t)
    fun Value { a, r } = !r

    (* constructor for Toggle *)
    fun Toggle b = let val r = ref b in { a = fn() => r:=not(!r), r = r } end

    (* constructor for NthToggle *)
    fun NthToggle (b, m) =
	let val ({ r, a }, n) = (Toggle b, ref 0)
	    fun a' () =
		let val i = !n+1 in if i>=m then (a(); n:=0) else n:=i end
	in { a = a', r = r } end

    (* activate n times, then print the value *)
    fun loop t = let fun l 0 = print (Bool.toString (Value t) ^ "\n")
		       | l i = (ignore (Value (Activate t)); l (i-1))
		 in l end

    (* try first for a Toggle object, then for an NthToggle object... *)
    fun main' n =
	(loop (Toggle true) n; loop (NthToggle (true, 3)) n; OS.Process.success)

    (* driver: *)
    fun main (_, []) = main' 1
      | main (_, arg :: _) = main' (getOpt (Int.fromString arg, 1))
end
