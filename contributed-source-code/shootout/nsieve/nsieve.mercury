% The Computer Language Benchmarks Game
% http://shootout.alioth.debian.org/
% Based on work by Anthony Borla
% Modified for Mercury by Glendon Holst

:- module nsieve.
:- interface.
:- import_module io.

:- pred main(io::di, io::uo) is det.

% ----------------------------------------------------------------------

:- implementation.

:- import_module array.
:- import_module list.
:- import_module int.
:- import_module bool.
:- import_module string.

% ----------------------------------------------------------------------

main(!IO) :-
	io.command_line_arguments(Args, !IO),
	(
		if
			Args = [AString|_],
			string.to_int(string.strip(AString), Aint)
		then
			N = Aint
		else
			N is 9
	),
	
	nsieve(10000 << N, !IO),
	nsieve(10000 << (N - 1), !IO),
	nsieve(10000 << (N - 2), !IO).

% ------------------------------- %

:- pred nsieve(int::in, io::di, io::uo) is det.

nsieve(N, !IO) :-
  Arr = array.init(N, yes),
  nsieve(2, Arr, 0, R),
  io.format("Primes up to %8u %8u\n", [i(N), i(R)], !IO).

% ------------------------------- %

:- pred nsieve(int::in, array(bool)::array_di, int::in, int::out) is det.

nsieve(Index, Arr, Acc, Result) :-
(
	Index = array.size(Arr) -> Result = Acc
;
	array.lookup(Arr, Index) = no -> nsieve(Index + 1, Arr, Acc, Result)
;
	clear_sieve(2 * Index, Index, Arr, Aout), 
	nsieve(Index + 1, Aout, Acc + 1, Result)
).


% ------------- %

:- pred clear_sieve(int, int, array(bool), array(bool)) is det.
:- mode clear_sieve(in, in, array_di, array_uo) is det.

clear_sieve(N, M, Arr, Aout) :-
(
	if N < array.size(Arr) then 
		array.set(Arr, N, no, A1), clear_sieve(N + M, M, A1, Aout)
	else
		Aout = Arr
).

% ------------------------------- %

