\ The Great Computer Language Shootout
\ http://shootout.alioth.debian.org/
\ 
\ contributed by Ian Osgood
\ NOTE: must run gforth with flags "-m 8M" for NUM = 9

\ read NUM from last command line argument
0. argc @ 1- arg >number 2drop drop 2 max constant NUM

: sieve ( size -- n )
  here over 1 fill
  0 over 2 do
    here i + c@ if 1+
      over here + here i 2* + over min ?do
        0 i c!
      j +loop
    then
  loop nip ;

\ count primes up to 2^n*10000
: test ( n -- )
  1 swap lshift 10000 *
  ." Primes up to " dup 8 u.r sieve 9 u.r cr ;

\ run sieve for N, N-1, N-2
  NUM test  NUM 1- test  NUM 2 - test

bye  \ done!
