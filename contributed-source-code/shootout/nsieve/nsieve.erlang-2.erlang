%  The Computer Language Benchmarks Game
%  http://shootout.alioth.debian.org/
%
%  contributed by Fredrik Svahn


-module(nsieve).
-export([main/1]).

main([Arg])->
    N = list_to_integer(Arg),
    [ go(10000 bsl X) || X <- [N, N-1, N-2] ],
    erlang:halt().

go(N) -> 
    C = ns(N, 2, 0, array:new({default, true})),
    io:fwrite("Primes up to ~8.10B ~8.10B~n", [N, C]).


ns(N, I, C, A) when I < N ->
    case array:get(I, A) of 
	false -> ns(N, I+1, C, A);
	true  -> ns(N, I+1, C+1, mark(I bsl 1, I, N, A))
    end;

ns(_, _, C, _) -> C. 


mark(J, I, N, A) when J < N -> 
    case array:get(J, A) of 
	false -> mark(J+I, I, N, A);
	true  -> mark(J+I, I, N, array:set(J, false, A))
    end;

mark(_, _, _, A) -> A.

