module:    partialsums
author:    Anthony Borla
synopsis:  partialsums benchmark
           The Great Computer Language Shootout
           http://shootout.alioth.debian.org/
           Based on D language implementation by D. Fladebo
copyright: public domain
use-libraries: common-dylan, io, transcendental
use-modules: common-dylan, format-out, transcendental

// --------------------------------

define function main() => ();
  let n = application-arguments()[0].string-to-integer;

  let s0 = 0.0d0; let s1 = 0.0d0; let s2 = 0.0d0; let s3 = 0.0d0;
  let s4 = 0.0d0; let s5 = 0.0d0; let s6 = 0.0d0; let s7 = 0.0d0;
  let s8 = 0.0d0; let d = 0.0d0; let alt = 1.0d0; let d = 1.0d0;

  while (d <= n)
    let d2 = d * d; let d3 = d2 * d; let ds = sin(d); let dc = cos(d);

    s0 := s0 + (2.0d0 / 3.0d0) ^ (d - 1.0d0);
    s1 := s1 + 1.0d0 / sqrt(d);
    s2 := s2 + 1.0d0 / (d * (d + 1.0d0));
    s3 := s3 + 1.0d0 / (d3 * ds * ds);
    s4 := s4 + 1.0d0 / (d3 * dc * dc);
    s5 := s5 + 1.0d0 / d;
    s6 := s6 + 1.0d0 / d2;
    s7 := s7 + alt / d;
    s8 := s8 + alt / (2.0d0 * d - 1.0d0);

    alt := -alt; d := d + 1.0d0;
  end while;

  local method padr(s :: <string>, padlen :: <integer>, c :: <character>) => (<string>);
    let reqlen = size(s) - padlen;
    if (reqlen < 0)
      s := concatenate(s, make(<string>, size: abs(reqlen), fill: c));
    end if;
    s;
  end method padr;

  format-out("%s\t(2/3)^k\n", padr(format-to-string("%.9f", s0), 9, '0'));
  format-out("%s\tk^-0.5\n", padr(format-to-string("%.9f", s1), 9, '0'));
  format-out("%s\t1/k(k+1)\n", padr(format-to-string("%.9f", s2), 9, '0'));
  format-out("%s\tFlint Hills\n", padr(format-to-string("%.9f", s3), 9, '0'));
  format-out("%s\tCookson Hills\n", padr(format-to-string("%.9f", s4), 9, '0'));
  format-out("%s\tHarmonic\n", padr(format-to-string("%.9f", s5), 9, '0'));
  format-out("%s\tRiemann Zeta\n", padr(format-to-string("%.9f", s6), 9, '0'));
  format-out("%s\tAlternating Harmonic\n", padr(format-to-string("%.9f", s7), 9, '0'));
  format-out("%s\tGregory\n", padr(format-to-string("%.9f", s8), 9, '0'));
end function main;

// --------------------------------

main();

