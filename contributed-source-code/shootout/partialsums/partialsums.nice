//   The Computer Language Shootout 
//   http://shootout.alioth.debian.org/
//   contributed by Isaac Gouy

import Include;

void main(String[] args){
   let n = Integer.parseInt(args[0]); 

   var a1 = 0.0, a2 = 0.0, a3 = 0.0, a4 = 0.0, a5 = 0.0, 
       a6 = 0.0, a7 = 0.0, a8 = 0.0, a9 = 0.0, alt = -1.0; 

   let twothirds = 2.0/3.0;
 
   for (int k=1; k<=n; k++){
      let double d = k;
      let k2 = d * d;
      let k3 = k2*d;
      let sk = Math.sin(k);
      let ck = Math.cos(k);
      alt = -alt;

      a1 += Math.pow(twothirds,k-1.0);
      a2 += 1.0/Math.sqrt(k);
      a3 += 1.0/(k*(k+1.0));
      a4 += 1.0/(k3 * sk*sk);
      a5 += 1.0/(k3 * ck*ck);
      a6 += 1.0/k;
      a7 += 1.0/k2;
      a8 += alt/k;
      a9 += alt/(2*k -1);
   }

   println( a1.toStringWith(9) + "\t(2/3)^k" );
   println( a2.toStringWith(9) + "\tk^-0.5" );
   println( a3.toStringWith(9) + "\t1/k(k+1)" );      
   println( a4.toStringWith(9) + "\tFlint Hills");  
   println( a5.toStringWith(9) + "\tCookson Hills");
   println( a6.toStringWith(9) + "\tHarmonic");
   println( a7.toStringWith(9) + "\tRiemann Zeta");
   println( a8.toStringWith(9) + "\tAlternating Harmonic");
   println( a9.toStringWith(9) + "\tGregory");
}
