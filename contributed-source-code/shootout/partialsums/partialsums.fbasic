'The Computer Language Shootout
'http://shootout.alioth.debian.org/
'contributed by Antoni Gual

#include "crt.bi"
option escape
dim sum(8) as double
dim as double k, k2, k3, ks, kc, tt
n = val(command$) + 1
if n = 0 then n = 25001
alt = 1.0
tt = 2 / 3

for k = 1 to n-1
   k2 = k * k
   k3 = k2 * k
   ks = sin(k)
   kc = cos(k)

   sum(0) += tt ^ (k - 1)
   sum(1) += 1 / sqr(k)
   sum(2) += 1 / (k * (k + 1))
   sum(3) += 1 / (k3 * ks * ks)
   sum(4) += 1 / (k3 * kc * kc)
   sum(5) += 1 / k
   sum(6) += 1 / k2
   sum(7) += alt / k
   sum(8) += alt / (2 * k - 1)
   alt *= -1
next k

printf("%.9f\t(2/3)^k\n", sum(0))
printf("%.9f\tk^-0.5\n", sum(1))
printf("%.9f\t1/k(k+1)\n",sum(2))
printf("%.9f\tFlint Hills\n", sum(3))
printf("%.9f\tCookson Hills\n", sum(4))
printf("%.9f\tHarmonic\n", sum(5))
printf("%.9f\tRiemann Zeta\n", sum(6))
printf("%.9f\tAlternating Harmonic\n", sum(7))
printf("%.9f\tGregory\n", sum(8))
