! The Computer Language Shootout
!   http://shootout.alioth.debian.org/
!
!   Contributed by Steve Decker
!   Version 3
!   Written similarly to the Oberon-2 version among others.
!
! compilation:
!   g95 -O3 -fomit-frame-pointer -funroll-loops partial3.f90
!   gfortran -O3 -fomit-frame-pointer -funroll-loops -ffast-math partial3.f90
!   ifort -O3 -ipo -static partial3.f90

program partial
  implicit none

  integer,   parameter :: dp = selected_real_kind(10)
  real(dp),  parameter :: TwoThirds = 2._dp/3._dp, Zero = 0._dp, One = 1._dp
  character, parameter :: Tab = achar(9)

  real(dp)         :: s1 = One, s2 = Zero, s3 = Zero, s4 = Zero, s5 = Zero,  &
                      s6 = Zero, s7 = Zero, s8 = Zero, s9 = Zero, alt = -One
  integer          :: n, k
  character(len=8) :: argv

  call get_command_argument(1, argv)
  read (argv, "(i8)") n

  do k = 1, n
     alt = -alt

     s1 = s1 + TwoThirds**real(k,dp)
     s2 = s2 + One / sqrt(real(k,dp))
     s3 = s3 + One / (k * (k + One))
     s4 = s4 + One / (real(k,dp)**3 * sin(real(k,dp))**2)
     s5 = s5 + One / (real(k,dp)**3 * cos(real(k,dp))**2)
     s6 = s6 + One / k
     s7 = s7 + One / (real(k,dp)**2)
     s8 = s8 + alt / k
     s9 = s9 + alt / (2*k - 1)
  end do
     
  write (*, "(f0.9,a)") s1, Tab // "(2/3)^k"
  write (*, "(f0.9,a)") s2, Tab // "k^-0.5"
  write (*,"(f11.9,a)") s3, Tab // "1/k(k+1)"
  write (*, "(f0.9,a)") s4, Tab // "Flint Hills"
  write (*, "(f0.9,a)") s5, Tab // "Cookson Hills"
  write (*, "(f0.9,a)") s6, Tab // "Harmonic"
  write (*, "(f0.9,a)") s7, Tab // "Riemann Zeta"
  write (*,"(f11.9,a)") s8, Tab // "Alternating Harmonic"
  write (*,"(f11.9,a)") s9, Tab // "Gregory"  
end program partial
