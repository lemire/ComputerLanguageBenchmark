/* ------------------------------------------------------------------ */
/* The Great Computer Language Shootout                               */
/* http://shootout.alioth.debian.org/                                 */
/*                                                                    */
/* Use of C library routines [requires interpreter GCI activation else*/
/* runtime error occurs]                                              */
/*                                                                    */
/* Contributed by Anthony Borla                                       */
/* ------------------------------------------------------------------ */

numeric digits 18

/* ----------------------------- */

call initSqrt ; call initSin ; call initCos

/* ----------------------------- */

N = ARG(1) ; if DATATYPE(N) \= 'NUM' | N < 1 then ; N = 1

NULL = "" ; ITEMS = "(2/3)^k k^-0.5 1/k(k+1) Flint_Hills Cookson_Hills Harmonic Riemann_Zeta Alternating_Harmonic Gregory"

ALT = 1 ; QUEUE_SIZE = WORDS(ITEMS)

do QUEUE_SIZE ; queue 0.0 ; end

do D = 1 for N
  D2 = D * D ; D3 = D2 * D ; DS = SIN(D) ; DC = COS(D)

  call update (2 / 3) ** (D - 1)
  call update 1 / SQRT(D)
  call update 1 / (D * (D + 1))
  call update 1 / (D3 * DS * DS)
  call update 1 / (D3 * DC * DC)
  call update 1 / D
  call update 1 / D2
  call update ALT / D
  call update ALT / (2 * D - 1)

  ALT = -ALT
end

do while ITEMS <> NULL
  parse var ITEMS CURRENT ITEMS ; parse pull V
  call print CURRENT, V
end

exit 0

/* ----------------------------- */

update : procedure
  parse pull V ; V = V + ARG(1) ; queue V
  return

/* ----------- */

print : procedure
  say LEFT(FORMAT(ARG(2),, 9), 16, " ") || CHANGESTR("_", ARG(1), " ")
  return

/* ----------------------------- */

initSqrt : procedure
  sqrt.calltype = 'cdecl as function with parameters'
  sqrt.return.type = 'float64'
  sqrt.return.name = 'return value'

  sqrt.0 = 1

  sqrt.1.type = 'float64'
  sqrt.1.name = 'number'

  LIBRARY = 'libm.so.6' ; parse source SYSTEM . .
  if SYSTEM == "WIN32" then ; LIBRARY = 'msvcrt' 

  err = rxFuncDefine('sqrt', LIBRARY, 'sqrt', 'sqrt.')

  if err \= 0 then
    say "Error registering function 'sqrt'"

  return

/* ----------------------------- */

initSin : procedure
  sin.calltype = 'cdecl as function with parameters'
  sin.return.type = 'float64'
  sin.return.name = 'return value'

  sin.0 = 1

  sin.1.type = 'float64'
  sin.1.name = 'number'

  LIBRARY = 'libm.so.6' ; parse source SYSTEM . .
  if SYSTEM == "WIN32" then ; LIBRARY = 'msvcrt' 

  err = rxFuncDefine('sin', LIBRARY, 'sin', 'sin.')

  if err \= 0 then
    say "Error registering function 'sin'"

  return

/* ----------------------------- */
    
initCos : procedure
  cos.calltype = 'cdecl as function with parameters'
  cos.return.type = 'float64'
  cos.return.name = 'return value'

  cos.0 = 1

  cos.1.type = 'float64'
  cos.1.name = 'number'

  LIBRARY = 'libm.so.6' ; parse source SYSTEM . .
  if SYSTEM == "WIN32" then ; LIBRARY = 'msvcrt' 

  err = rxFuncDefine('cos', LIBRARY, 'cos', 'cos.')

  if err \= 0 then
    say "Error registering function 'cos'"

  return

