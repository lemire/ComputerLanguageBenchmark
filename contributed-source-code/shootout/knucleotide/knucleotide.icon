# The Computer Language Shootout
# http://shootout.alioth.debian.org/
# by Charles Evans 2006-1-4

link printf

global seq, freq
procedure calc_frequencies(len)
local lt
  lt:=\freq[len]|(freq[len]:=table(0))
  if len=1 then every lt[!seq]+:=1
  else every lt[seq[1 to *seq-len+1 +:len]]+:=1
  return lt
end

procedure write_frequencies(len)
local t,n,l,i,k,x
n:=100.0/(1+*seq-len)
t:=\freq[len] | calc_frequencies(len)
l:=[]
#write(n)
#every k:=key(t) do write(t[k],"\t",t[k]*n,"\t",k)
every k:=key(t) do 
	put(l,sprintf("%17.13r %s",t[k]*n,k))
l:=sort(l)
while x:=pull(l) do 
   printf("%s %.3r\n",x[-len:0],x[1:-len])
write()
end

procedure write_count(s)
t:=\freq[*s]|calc_frequencies(*s)
write(t[s],"\t",s)
end

procedure main(argv)
freq:=table()
seq:=""
while l:= read() & not( l ? =">THREE" )
while l:= read() & not( l ? =">" ) do
   l ? =";" | (seq||:=l)
seq:=map(seq,&lcase,&ucase)
write_frequencies(1)
write_frequencies(2)
write_count( "GGT")
write_count( "GGTA")
write_count( "GGTATT")
write_count( "GGTATTTTAATT")
write_count( "GGTATTTTAATTTATAGT")
#write(&errout,&time) # fasta 250000 in fasta.250: 19sec
end
