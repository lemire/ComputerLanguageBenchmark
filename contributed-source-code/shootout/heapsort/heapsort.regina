/* The Great Computer Language Shootout
   http://shootout.alioth.debian.org/

   Contributed by Dennis Decker Jensen
*/

numeric digits 11 /* 15 digits precision not needed here */
IM = 139968; IA = 3877; IC = 29573; LAST = 42

arg n . ; if n = '' then n = 1

do i = 1 for n
	ary.i = gen_random(1)
end

call heapsort n

say format(ary.n, , 10)
exit(0)

gen_random: procedure expose IM IA IC LAST
LAST = (LAST * IA + IC) // IM
return arg(1) * LAST / IM

heapsort: procedure expose ary.
arg n .
ir = n
l = trunc(0.5 + n % 2) + 1

do forever
	if l > 1 then do
		l = l - 1
		rra = ary.l
	end
	else do
		rra = ary.ir
		ary.ir = ary.1
		ir = ir - 1
		if ir = 1 then do
			ary.1 = rra
			return
		end
	end
	i = l
	j = l * 2
	do while j <= ir
		k = j + 1
		if j < ir & ary.j < ary.k then j = k
		if rra < ary.j then do
			ary.i = ary.j
			i = j
			j = j + j
		end
		else j = ir + 1
	end
	ary.i = rra
end

