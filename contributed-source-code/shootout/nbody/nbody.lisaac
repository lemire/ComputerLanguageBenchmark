/* The Computer Language Benchmarks Game
   http://shootout.alioth.debian.org/
   contributed by Xavier Oswald & Anthony Pajot
*/

// SPLITFILE=nbody.li
Section Header

  + name := NBODY;

Section Inherit

  - parent_object : OBJECT := OBJECT;

Section Private

  - nbodies : INTEGER;

  - bodies:NATIVE_ARRAY[Expanded PLANET];

  - b:Strict PLANET;
  - b2:Strict PLANET;
  - dx:REAL_64;
  - dy:REAL_64;
  - dz:REAL_64;
  - distance:REAL_64;
  - mag : REAL_64;

Section External

  - advance (dt:REAL_64,n:INTEGER) <-
  (
    0.to n do { i : INTEGER;
      b := bodies.item i.this;

      (i + 1).to n do { j : INTEGER;

        b2 := bodies.item j.this;

        dx := b.x - b2.x;
        dy := b.y - b2.y;
        dz := b.z - b2.z;

        distance := (dx * dx + dy * dy + dz * dz).sqrt;

        mag := dt / (distance * distance * distance);

        b.set_vx (b.vx - dx * b2.mass * mag);
        b.set_vy (b.vy - dy * b2.mass * mag);
        b.set_vz (b.vz - dz * b2.mass * mag);

        b2.set_vx (b2.vx + dx * b.mass * mag);
        b2.set_vy (b2.vy + dy * b.mass * mag);
        b2.set_vz (b2.vz + dz * b.mass * mag);
      };
    };
    0.to n do { i : INTEGER;
      b := bodies.item i.this;

      b.set_x (b.x + dt * b.vx);
      b.set_y (b.y + dt * b.vy);
      b.set_z (b.z + dt * b.vz);
    };
  );

  - energy n:INTEGER :REAL_64 <-
  ( + e : REAL_64;
    + b : Strict PLANET;
    + b2 : Strict PLANET;
    + dx, dy, dz : REAL_64;
    + distance : REAL_64;

    0.to nbodies do { i : INTEGER;
      b := bodies.item i.this;
      e := e + b.mass * 0.5 * (b.vx * b.vx + b.vy * b.vy + b.vz * b.vz);

      (i + 1).to nbodies do { j : INTEGER;
        b2 := bodies.item j.this;

        dx := b.x - b2.x;
        dy := b.y - b2.y;
        dz := b.z - b2.z;

        distance := (dx * dx + dy * dy + dz * dz).sqrt;

        e := e - (b.mass * b2.mass) / distance;
      };
    };

    e
  );

  - offset_momentum n:INTEGER <-
  ( + px, py, pz : REAL_64;
    + b : Strict PLANET;
    0.to n do { i : INTEGER;
      b := bodies.item i.this;
      px := px + b.vx * b.mass;
      py := py + b.vy * b.mass;
      pz := pz + b.vz * b.mass;
    };

    bodies.first.set_speed_to (
      - px / PLANET.solar_mass,
      - py / PLANET.solar_mass,
      - pz / PLANET.solar_mass
    );

  );
Section Public

  - main <-
  ( + n:INTEGER;

    n := COMMAND_LINE.item 1.to_integer;
    nbodies := 4;
    bodies  := NATIVE_ARRAY[Expanded PLANET].calloc_intern nbodies;

    bodies.item 0.make_sun;
    bodies.item 1.make_jupiter;
    bodies.item 2.make_saturn;
    bodies.item 3.make_uranus;
    bodies.item 4.make_neptune;

    offset_momentum nbodies;

    energy nbodies.print_format_c "%.9f\n";

    1.to n do { i:INTEGER;
      advance (`0.01`:REAL_64,nbodies);
    };

    energy nbodies.print_format_c "%.9f\n";
  );
// class NBODY

// SPLITFILE=planet.li
Section Header

  + name := PLANET;

Section Inherit

  - parent_object : OBJECT := OBJECT;

Section Public

  - this:Strict PLANET <- `&@Self`:Strict PLANET;

  - pi : REAL_64 := `3.141592653589793`:REAL_64;

  - solar_mass:REAL_64 := pi * pi * 4.0;

  - days_per_year:REAL_64 := 365.24;

  + x : REAL_64;
  + y : REAL_64;
  + z : REAL_64;

  + px : REAL_64;
  + py : REAL_64;
  + pz : REAL_64;

  + vx : REAL_64;
  + vy : REAL_64;
  + vz : REAL_64;

  + pvx : REAL_64;
  + pvy : REAL_64;
  + pvz : REAL_64;

  + mass : REAL_64;

  - set_x v:REAL_64 <-
  (
    x := v;
  );
  - set_y v:REAL_64 <-
  (
    y := v;
  );
  - set_z v:REAL_64 <-
  (
    z := v;
  );

  - set_vx v:REAL_64 <-
  (
    vx := v;
  );
  - set_vy v:REAL_64 <-
  (
    vy := v;
  );
  - set_vz v:REAL_64 <-
  (
    vz := v;
  );

  - set_speed_to (pvx, pvy, pvz:REAL_64) <-
  (
    vx := pvx;
    vy := pvy;
    vz := pvz;
  );

  //different planets used in the test.

  - make_sun <-
  (
    mass := solar_mass;
  );

  - make_jupiter <-
  (
    x := `4.84143144246472090E+00`:REAL_64;
    y := `-1.16032004402742839E+00`:REAL_64;
    z := `-1.03622044471123109E-01`:REAL_64;

    vx := `1.66007664274403694E-03`:REAL_64 * days_per_year;
    vy := `7.69901118419740425E-03`:REAL_64 * days_per_year;
    vz := `-6.90460016972063023E-05`:REAL_64 * days_per_year;

    mass := `9.54791938424326609E-04`:REAL_64 * solar_mass;
  );

  - make_saturn <-
  (
    x := `8.34336671824457987E+00`:REAL_64;
    y := `4.12479856412430479E+00`:REAL_64;
    z := `-4.03523417114321381E-01`:REAL_64;

    vx := `-2.76742510726862411E-03`:REAL_64 * days_per_year;
    vy := `4.99852801234917238E-03`:REAL_64 * days_per_year;
    vz := `2.30417297573763929E-05`:REAL_64 * days_per_year;

    mass := `2.85885980666130812E-04`:REAL_64 * solar_mass;
  );

  - make_uranus <-
  (
    x := `1.28943695621391310E+01`:REAL_64;
    y := `-1.51111514016986312E+01`:REAL_64;
    z := `-2.23307578892655734E-01`:REAL_64;

    vx := `2.96460137564761618E-03`:REAL_64 * days_per_year;
    vy := `2.37847173959480950E-03`:REAL_64 * days_per_year;
    vz := `-2.96589568540237556E-05`:REAL_64 * days_per_year;

    mass := `4.36624404335156298E-05`:REAL_64 * solar_mass;
  );

  - make_neptune <-
  (
    x := `1.53796971148509165E+01`:REAL_64;
    y := `-2.59193146099879641E+01`:REAL_64;
    z := `1.79258772950371181E-01`:REAL_64;

    vx := `2.68067772490389322E-03`:REAL_64  * days_per_year;
    vy := `1.62824170038242295E-03`:REAL_64  * days_per_year;
    vz := `-9.51592254519715870E-05`:REAL_64 * days_per_year;

    mass := `5.15138902046611451E-05`:REAL_64 * solar_mass;
  );
// class PLANET

