# ----------------------------------------------------------------------
# The Great Computer Language Shootout
# http://shootout.alioth.debian.org/
#
# Contributed by Anthony Borla
# ----------------------------------------------------------------------

link printf

procedure main(argv)
  local N ; N := +(argv[1]) | 100

  printf("%.9r\n", approximate(N))
end

# ---------------------------------

procedure approximate(n)
  local u, v, vBv, vV, i ; u := list(n, 1.0) ; v := list(n, 0.0) ; vBv := 0.0 ; vV := 0.0

  every i := 1 to 10 do { mulAtAv(n, u, v) ; mulAtAv(n, v, u) }
  every i := 1 to n do { vBv +:= u[i] * v[i] ; vV +:= v[i] * v[i] }

  return sqrt(vBv / vV)
end

# ---------------

procedure A(i, j)
  i -:= 1.0 ; j -:= 1.0
  return 1.0 / ((i + j) * ( i + j + 1.0) / 2.0 + i + 1.0)
end

# ---------------

procedure mulAv(n, v, av)
  local i, j
  every i := 1 to n do { av[i] := 0.0 ; every j := 1 to n do { av[i] +:= A(i, j) * v[j] } }
end

# ---------------

procedure mulAtV(n, v, atv)
  local i, j
  every i := 1 to n do { atv[i] := 0.0 ; every j := 1 to n do { atv[i] +:= A(j, i) * v[j] } }
end

# ---------------

procedure mulAtAv(n, v, atav)
  local i, u ; u := list(n, 0.0)
  mulAv(n, v, u) ; mulAtV(n, u, atav)
end

