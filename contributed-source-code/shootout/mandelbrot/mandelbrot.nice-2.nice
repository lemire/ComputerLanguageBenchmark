/* The Computer Language Shootout 
   http://shootout.alioth.debian.org/
   contributed by Isaac Gouy
*/

import java.io.*;

void main(String[] args){
   let width = Integer.parseInt(args[0]);   
   let height = width; 
   println("P4\n" + width + " " + height);

   let s = new BufferedOutputStream(System.out);
   let limitSquared = 4.0;
   let iterations = 50;
   var isOverLimit = false;
   var bits = 0; var bitnum = 0; 

   for(var y=0; y<height; y++){
      for(var x=0; x<width; x++){

         var Zr = 0.0; var Zi = 0.0;
         let Cr = (2.0*x/width - 1.5); let Ci = (2.0*y/height - 1.0);

         var i = 0;
         do {
            let Tr = Zr*Zr - Zi*Zi + Cr;
            let Ti = 2.0*Zr*Zi + Ci;
            Zr = Tr; Zi = Ti;
            isOverLimit = Zr*Zr + Zi*Zi > limitSquared;
         } while (!isOverLimit && (++i < iterations));

         bits = bits << 1;
         if (!isOverLimit) bits++;
         bitnum++;

         if (x == width - 1) {
            bits = bits << (8 - bitnum);
            bitnum = 8;
         }

         if (bitnum == 8){
            let byte b = cast(bits); s.write(b);
            bits = 0; bitnum = 0;
         }
      }
   } 
   s.close;
}
