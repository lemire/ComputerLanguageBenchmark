;;; The Computer Language Benchmarks Game
;;; http://shootout.alioth.debian.org/
;;;
;;; contributed by Anthony Borla
;;; modified by Tobia Conforto

(define-constant +limit-sqr+ 4.0)

(define-constant +iterations+ 50)

(define (mandelbrot x y n)
  (let ((cr (- (/ (* 2.0 x) n) 1.5))
        (ci (- (/ (* 2.0 y) n) 1.0)))
    (let loop ((i 0)
               (zr 0.0)
               (zi 0.0))
      (let ((zrq (fp* zr zr))
            (ziq (fp* zi zi)))
        (cond
         ((fx> i +iterations+) 1)
         ((fp> (fp+ zrq ziq) +limit-sqr+) 0)
         (else
          (loop (fx+ i 1)
                (fp+ cr (fp- zrq ziq))
                (fp+ ci (fp* 2.0 (fp* zr zi))))))))))

(define (main n)
  (print "P4")
  (print n " " n)
  (do ((y 0 (fx+ y 1)))
      ((fx= y n) #f)
    (let loop-x ((x 0)
                 (bitnum 0)
                 (bits 0))
      (cond
       ((fx= x n)
        (if (fx> bitnum 0)
            (write-char (integer->char (fxshl bits (fx- 8 (fxmod n 8)))))
            #f))
       ((fx= bitnum 8)
        (write-char (integer->char bits))
        (loop-x (fx+ x 1)
                1
                (mandelbrot x y n)))
       (else
        (loop-x (fx+ x 1)
                (fx+ bitnum 1)
                (fxior (fxshl bits 1)
                       (mandelbrot x y n))))))))

(main (string->number (car (command-line-arguments))))
