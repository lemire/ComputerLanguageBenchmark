# ----------------------------------------------------------------------
# The Great Computer Language Shootout
# http://shootout.alioth.debian.org/
#
# Contributed by Anthony Borla
# ----------------------------------------------------------------------

$define ITERATIONS 50
$define LIMIT_SQR 4.0

$define NO 0
$define YES 1

procedure main(argv)
  local N, x, y, bit_num, byte_acc, ZR, ZI, TR, TI, CR, CI, LIMIT_ADJ, OUTPUT, i

  N := (argv[1] | 10) ; bit_num := 0 ; byte_acc := 0 ; OUTPUT := NO

  write("P4\n", N, " ", N)

  every y := 0 to (N - 1) do
  {
    every x := 0 to (N - 1) do
    {
      ZR := ZI := TR := TI := 0.0
      CR := 2.0 * x / N - 1.5 ; CI := 2.0 * y / N - 1.0

      LIMIT_ADJ := 1 ; i := 0

      while i < ITERATIONS do
      {
        TR := ZR * ZR - ZI * ZI + CR ; TI := 2.0 * ZR * ZI + CI
        ZR := TR ; ZI := TI

        if ZR * ZR + ZI * ZI > LIMIT_SQR then { LIMIT_ADJ := 0 ; break }
        
        i +:= 1
      }

      bit_num +:= 1 ; if bit_num = 8 then OUTPUT := YES

      byte_acc := byte_acc * 2 + LIMIT_ADJ

      if x = (N - 1) & bit_num ~= 8 then
      {
        byte_acc := byte_acc * (2 ^ (8 - N % 8)) ; OUTPUT := YES
      }

      if OUTPUT = YES then
      {
        writes(char(byte_acc))
        byte_acc := bit_num := 0 ; OUTPUT := NO
      }
    }
  }

end

