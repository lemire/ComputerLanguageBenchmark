rem The Computer Language Shootout
rem http://shootout.alioth.debian.org/
rem contributed by Antoni Gual

#include "crt.bi"

dim as integer perm(20), permp(20), count(20)
dim as integer n,print30=0, m, r, j, i, k, temp, mostFlips=0, flips, perm0

n = val(command$)
if n < 1 then n = 7

m = n - 1
for i = 0 to m : permp(i) = i : next i
r = n
do
  if print30 < 30 then
     for i = 0 to m
        printf ("%d",permp(i) + 1)
     next i
     printf("%c", 10)
     print30 += 1
  end if
  while r <> 1
     count(r-1) = r
     r -= 1
  wend
  if not ((permp(0)=0) or (permp(m)=m)) then
     for i = 0 to m
        perm(i) = permp(i)
     next i
     flips = 0
     k = perm(0)
     do
       i=1
       j=k-1
       while i<j
           temp = perm(i)
           perm(i)= perm(j)
           perm(j)= temp
           i += 1
           j -= 1
       wend
       flips += 1
       j=perm(k)
       perm(k)=k
       k=j
     loop until k = 0
     if flips > mostFlips then mostFlips = flips
  end if
  Do
     if r = n then
        printf ("Pfannkuchen(%d) = %d%c",n, mostflips, 10)
        end
     else
        perm0 = permp(0)
        i = 0
        k=1
        while i < r
           permp(i) = permp(k)
           i+= 1
           k+=1
        wend
        permp(r) = perm0
        count(r) -= 1
        if count(r) > 0 then exit Do
        r += 1
     end if
  Loop
loop while 1