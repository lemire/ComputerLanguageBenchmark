# The Great Computer Language Shootout
#   http://shootout.alioth.debian.org/
# Icon moments
# by Charles Evans 2005-12-24
# based on moments.nice by Isaac Gouy

link printf

procedure main(argv)#{
   num := 0.0; sum := 0.0; numbers:=[]
   while ((line := read()))do{
         num := +line;
         sum +:= num;
         put(numbers,num);
   }
   n := *numbers; if (n<1) then return;
   mean := sum/n;
   median := average_deviation := standard_deviation := variance := 0.0
   skew := kurtosis := deviation := 0.0

   every each:=!numbers do{
      deviation := each - mean;
      average_deviation +:= abs(deviation);
      variance +:= deviation * deviation;
      skew +:= deviation ^ 3;
      kurtosis +:= deviation ^ 4;
   }

   average_deviation /:= n;
   variance /:= (n - 1);
   standard_deviation := sqrt(variance);
   if (variance ~= 0.0) then{
      skew /:= (n * variance * standard_deviation);
      kurtosis := kurtosis/(n * variance * variance) - 3.0;
   }

   numbers:=sort(numbers)

   mid := n/2;
   if (n > 0) then
      median := if(n % 2 ~= 0) then
         numbers[mid+1] else (numbers[mid] + numbers[mid+1])/2.0;

   writes("n:                  "); write(n);
   printf("median:             %.6r\n" ,median)
   printf("mean:               %.6r\n" ,mean)
   printf("average_deviation:  %.6r\n" ,average_deviation)
   printf("standard_deviation: %.6r\n" ,standard_deviation)
   printf("variance:           %.6r\n" ,variance)
   printf("skew:               %.6r\n" ,skew)
   printf("kurtosis:           %.6r\n" ,kurtosis)
write(&errout,&time)
end