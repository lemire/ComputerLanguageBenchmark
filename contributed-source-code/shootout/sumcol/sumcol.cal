/** 
 * The Computer Language Benchmarks Game 
 * Based on the CAL Open Quark version
 * Contributed by Magnus Byne.
 */
module Sumcol;

import Cal.Core.Prelude using
    typeConstructor = Int, String;
    function = eager, intToString, stringToInt;
    ;

import Cal.IO.Console using
    typeConstructor = JBufferedReader;
    function = isNullString, makeBufferedReader, printLine, readLine, stdin;
    ;

main :: [String] -> ();
public main args =
    let
        loop :: JBufferedReader -> Int -> Int;
        loop !input !sum =
            let
                line = eager $ readLine input;
            in
                if isNullString line then
                    sum
                else
                    loop input (sum + stringToInt line)
            ;
    in
        printLine (intToString (loop (makeBufferedReader stdin) 0))
    ;

