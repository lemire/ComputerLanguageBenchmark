%% The Computer Language Benchmark Games
%% http://shootout.alioth.debian.org/
%% Contributed by Ulf Wiger

-module(sumcol).
-export([main/1]).
-export([start/0]).

%% get the program argument, which is how many test iterations to run
%% for this test, we ignore this parameter
main(_Args) ->
    user ! {self(), wait_ready},
    receive
	ready ->
	    halt()
    end.

%% callback function for user-defined line-oriented io
start() ->
    spawn(fun() ->
		  process_flag(priority,high),
		  register(user, self()),
		  read_in(open_port({fd,0,1}, [{line,128},eof]), 0)
	  end).

read_in(Port, Sum) ->
    receive
	{Port, {data, {Eol, Str}}} when Eol==eol; Eol==noeol->
	    read_in(Port, Sum + list_to_integer(Str));
	{Port, eof}  ->
	    ready(Port, Sum)
    end.

ready(Port, Sum) ->
    port_command(Port, [integer_to_list(Sum), "\n"]),
    erlang:port_close(Port),
    receive
	{From, wait_ready} ->
	     From ! ready
    end.
